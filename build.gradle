plugins {
  id "java-library"
  id "maven"
  id "net.ltgt.errorprone" version "0.0.13"
  id "com.github.sherter.google-java-format" version "0.6"
  id "com.github.hierynomus.license" version "0.14.0"
}

group = "org.gwtproject.http"
version = "HEAD-SNAPSHOT"

repositories {
  mavenCentral()
  maven { url = "https://oss.sonatype.org/content/repositories/google-snapshots/"}
}

dependencies {
  errorprone "com.google.errorprone:error_prone_core:2.2.0"

  implementation "com.google.elemental2:elemental2-dom:1.0.0-RC1"
  implementation "com.google.elemental2:elemental2-core:1.0.0-RC1"
  implementation "com.google.jsinterop:base:1.0.0-RC1"

  testImplementation "junit:junit:4.12"
  testImplementation "com.google.gwt:gwt-user:2.8.2"
  testImplementation "com.google.gwt:gwt-dev:2.8.2"
}

sourceCompatibility = JavaVersion.VERSION_1_8
tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
  options.compilerArgs << "-Werror" << "-Xlint:all" << "-Xep:StringSplitter:OFF"
  if (JavaVersion.current().isJava9Compatible()) {
    options.compilerArgs << "--release" << "8"
  }
}

jar {
  from sourceSets.main.allJava
}

test {
  def warDir = file("$buildDir/gwt/www-test")
  def workDir = file("$buildDir/gwt/work")
  def cacheDir = file("$buildDir/gwt/cache")
  doFirst {
    mkdir(warDir)
    mkdir(workDir)
    mkdir(cacheDir)
  }

  classpath += sourceSets.main.allJava.sourceDirectories + sourceSets.test.allJava.sourceDirectories
  include "**/*Suite.class"
  systemProperty("gwt.args", "-ea -draftCompile -batch module -war \"$warDir\" -workDir \"$workDir\" -runStyle HtmlUnit:Chrome")
  systemProperty("gwt.persistentunitcachedir", cacheDir)
}

javadoc {
  options.addBooleanOption("Xdoclint:all,-missing", true)
}

googleJavaFormat {
  toolVersion = "1.5"
}

license {
  header = rootProject.file('LICENSE.header')
  encoding = 'UTF-8'
  skipExistingHeaders = true
  mapping java: 'SLASHSTAR_STYLE'

  ext.year = java.time.Year.now()
  ext.name = 'The GWT Project Authors'
}
